
@top TraceQL {
    SpansetPipelineExpression
}

@precedence {
    Gt @left
    Desc @left
    tilde @left
    fieldOp @left
    comparisonOp @left
    scalarOp @left
    And @left
    Or @left
    Pipe @left
    prefix
    close_paren
}

SpansetPipelineExpression {
    "(" SpansetPipelineExpression !close_paren ")" |
    SpansetPipelineExpression !And And SpansetPipelineExpression |
    SpansetPipelineExpression !Gt Gt SpansetPipelineExpression |
    SpansetPipelineExpression !Desc Desc SpansetPipelineExpression |
    SpansetPipelineExpression !Or Or SpansetPipelineExpression |
    SpansetPipelineExpression !tilde tilde SpansetPipelineExpression |
    SpansetPipelineExpression !Pipe Pipe SpansetPipelineExpression |
    WrappedSpansetPipeline |
    SpansetPipeline
}

WrappedSpansetPipeline {
    "(" SpansetPipeline !close_paren ")"
}

SpansetPipeline {
    SpansetExpression |
    SpansetFilter |
    ScalarFilter |
    GroupOperation |
    SelectOperation |
    CoalesceOperation
}

GroupOperation {
    "by" "(" FieldExpression ")"
}

CoalesceOperation {
    "coalesce" "()"
}

SelectOperation {
    "select" "(" SelectArgs ")"
}

SelectArgs {
    FieldExpression |
    SelectArgs "," FieldExpression
}

SpansetExpression {
    "(" SpansetExpression !close_paren ")" |
    SpansetExpression !And And SpansetExpression |
    SpansetExpression !Gt Gt SpansetExpression |
    SpansetExpression !Desc Desc SpansetExpression |
    SpansetExpression !Or Or SpansetExpression |
    SpansetExpression !tilde tilde SpansetExpression
}

SpansetFilter {
    "{" "}" |
    "{" FieldExpression "}"
}

ScalarFilter {
    ScalarExpression !comparisonOp ComparisonOp ScalarExpression
}

ScalarExpression {
    "(" ScalarExpression !close_paren ")" |
    ScalarExpression !scalarOp ScalarOp ScalarExpression |
    Aggregate |
    Integer |
    Float |
    Duration |
    TemplateVariable | 
    !prefix "-" Integer |
    !prefix "-" Float |
    !prefix "-" Duration
}

Aggregate {
    "count()" |
    AggregateExpression "(" FieldExpression ")"
}

AggregateExpression { "max" | "min" | "avg" | "sum" }

FieldExpression {
    "(" FieldExpression ")" |
    FieldExpression !fieldOp FieldOp FieldExpression |
    FieldExpression !And And FieldExpression |
    FieldExpression !Or Or FieldExpression |
    !prefix "-" FieldExpression |
    !prefix "!" FieldExpression |
    Static |
    IntrinsicField |
    AttributeField
}

Static {
    String |
    Integer |
    Float |
    "true" |
    "false" |
    "nil" |
    Duration |
    TemplateVariable | 
    "ok" |
    "error" |
    "unset" |
    "unspecified" |
    "internal" |
    "server" |
    "client" |
    "producer" |
    "consumer"
}

IntrinsicField {
    "duration" |
    "childCount" |
    "name" |
    "status" |
    "kind" |
    Parent |
    "rootName" |
    "rootServiceName" |
    "traceDuration"
}

AttributeField {
    "." Identifier |
    Resource "." Identifier |
    Span "." Identifier |
    Parent "." Identifier |
    Parent "." Resource "." Identifier |
    Parent "." Span "." Identifier
}


@tokens {
    identifierExpression { $[a-zA-Z_.]$[a-zA-Z0-9_.]* }
    templateVariableWithBraces {  
        "{" identifierExpression "}" | 
        "{" identifierExpression ":" $[a-zA-Z0-9_.]+ "}"  
    }

    whitespace { std.whitespace+ }
    String { '"' (![\\\n"]+)* '"' }
    Integer { @digit+ }
    Float { @digit+ "." @digit+ }
    Duration { @digit+ ("ms"|"s"|"m"|"h") }

    @precedence { Float, Duration, Integer }

    Identifier { identifierExpression | "$" templateVariableWithBraces }
    TemplateVariable { "$" identifierExpression | "$" templateVariableWithBraces }

    And { "&&" }
    Or { "||" }
    Gt { ">" }
    Desc { ">>" }
    tilde { "~" }
    Pipe { "|" }
    ComparisonOp { "=" | "!=" | "<" | "<=" | ">" | ">=" | "=~" | "!~" }
    ScalarOp {"+" | "-" | "*" | "/" | "%" | "^"  }
    FieldOp { ComparisonOp | ScalarOp | tilde }

    Parent { "parent" }
    Resource { "resource" }
    Span { "span" }

    @precedence { Parent, Resource, Span, Identifier }
}

@skip {
    whitespace
}