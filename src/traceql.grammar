
@top TraceQL {
    SpansetPipelineExpression
}

@precedence {
    Gt @left
    Lt @left
    Desc @left
    Anc @left
    tilde @left
    fieldOp @left
    comparisonOp @left
    scalarOp @left
    And @left
    Or @left
    Pipe @left
    ExperimentalOp @left
    prefix
    close_paren
}

SpansetPipelineExpression {
    "(" SpansetPipelineExpression !close_paren ")" |
    SpansetPipelineExpression !And And SpansetPipelineExpression |
    SpansetPipelineExpression !Gt Gt SpansetPipelineExpression |
    SpansetPipelineExpression !Desc Desc SpansetPipelineExpression |
    SpansetPipelineExpression !Lt Lt SpansetPipelineExpression |
    SpansetPipelineExpression !Anc Anc SpansetPipelineExpression |
    SpansetPipelineExpression !Or Or SpansetPipelineExpression |
    SpansetPipelineExpression !tilde tilde SpansetPipelineExpression |
    SpansetPipelineExpression !Pipe Pipe SpansetPipelineExpression |
    SpansetPipelineExpression !ExperimentalOp ExperimentalOp SpansetPipelineExpression |
    WrappedSpansetPipeline |
    SpansetPipeline
}

WrappedSpansetPipeline {
    "(" SpansetPipeline !close_paren ")"
}

SpansetPipeline {
    SpansetExpression |
    SpansetFilter |
    ScalarFilter |
    GroupOperation |
    SelectOperation |
    CoalesceOperation |
    MetricsOperation
}

GroupOperation {
    "by" "(" FieldExpression ")"
}

CoalesceOperation {
    "coalesce" "()"
}

SelectOperation {
    "select" "(" SelectArgs ")"
}

SelectArgs {
    FieldExpression |
    SelectArgs "," FieldExpression
}

SpansetExpression {
    "(" SpansetExpression !close_paren ")" |
    SpansetExpression !And And SpansetExpression |
    SpansetExpression !Gt Gt SpansetExpression |
    SpansetExpression !Lt Lt SpansetExpression |
    SpansetExpression !Desc Desc SpansetExpression |
    SpansetExpression !Anc Anc SpansetExpression |
    SpansetExpression !Or Or SpansetExpression |
    SpansetExpression !tilde tilde SpansetExpression
}

SpansetFilter {
    "{" "}" |
    "{" FieldExpression "}"
}

ScalarFilter {
    ScalarExpression !comparisonOp ComparisonOp ScalarExpression
}

ScalarExpression {
    "(" ScalarExpression !close_paren ")" |
    ScalarExpression !scalarOp ScalarOp ScalarExpression |
    Aggregate |
    Integer |
    Float |
    Duration |
    TemplateVariable | 
    !prefix "-" Integer |
    !prefix "-" Float |
    !prefix "-" Duration
}

Aggregate {
    "count()" |
    AggregateExpression "(" FieldExpression ")"
}

AggregateExpression { "max" | "min" | "avg" | "sum" }

FieldExpression {
    "(" FieldExpression ")" |
    FieldExpression !fieldOp FieldOp FieldExpression |
    FieldExpression !And And FieldExpression |
    FieldExpression !Or Or FieldExpression |
    !prefix "-" FieldExpression |
    !prefix "!" FieldExpression |
    Static |
    IntrinsicField |
    AttributeField
}

MetricsOperation {
    MetricsOperationType "()" |
    MetricsOperationType "(" SelectArgs ")" |
    MetricsOperationType "()" GroupOperation | 
    MetricsOperationType "(" SelectArgs ")" GroupOperation
}

MetricsOperationType { "rate" | "count_over_time" | "avg_over_time" | "max_over_time" | "min_over_time" | "quantile_over_time" }

Static {
    String |
    Integer |
    Float |
    "true" |
    "false" |
    "nil" |
    Duration |
    TemplateVariable | 
    "ok" |
    "error" |
    "unset" |
    "unspecified" |
    "internal" |
    "server" |
    "client" |
    "producer" |
    "consumer"
}

IntrinsicField {
    "childCount" |
    "duration" |
    "kind" |
    "name" |
    "rootName" |
    "rootServiceName" |
    "status" |
    "statusMessage" |
    "traceDuration" |
    Parent
}

AttributeField {
    "." Identifier |
    Resource "." Identifier |
    Span "." Identifier |
    Parent "." Identifier |
    Parent "." Resource "." Identifier |
    Parent "." Span "." Identifier
}

@tokens {
    // All caracters but whitespace and those listed at the following link are valid:
    // https://github.com/grafana/tempo/blob/b29c9413358da6e1b66c2930991eca2f10d9ec88/pkg/traceql/lexer.go#L350
    // Some valid queries are currently not handled by the parser, because there are some issues in writing the correct regex.
    // We should handle this, and potentially also simplify the regex.
    identifierExpression { (('"'  (![\\"] | "\\\\" | '\\"')* '"' ) | $[a-zA-Z_@#$%*-+?:;'`]) (('"'  (![\\"] | "\\\\" | '\\"')* '"' ) | ![^{}()=~!<>&|," ])* }
    templateVariableWithBraces {  
        "{" identifierExpression "}" | 
        "{" identifierExpression ":" $[a-zA-Z0-9_.]+ "}"  
    }

    whitespace { std.whitespace+ }
    stringBacktick { "`" ![`]* "`" }
    stringDouble { '"' (!["\\] | '\\"' | "\\\\")* '"' }  // single quote (') strings are currently not supported

    String { (stringDouble) | (stringBacktick) }
    Integer { @digit+ }
    Float { @digit+ "." @digit+ }
    Duration { (Integer | Float) ("us"|"Âµs"|"ns"|"ms"|"s"|"m"|"h") }

    @precedence { Duration, Float, Integer }

    Identifier { identifierExpression | "$" templateVariableWithBraces }
    TemplateVariable { "$" identifierExpression | "$" templateVariableWithBraces }

    And { "&&" }
    Or { "||" }
    Gt { ">" }
    Lt { "<" }
    Desc { ">>" }
    Anc { "<<" }
    tilde { "~" }
    Pipe { "|" }
    ComparisonOp { "=" | "!=" | "<" | "<=" | ">" | ">=" | "=~" | "!~" }
    ExperimentalOp { "!>" | "!>>" | "!<" | "!<<" | "!~" }
    ScalarOp {"+" | "-" | "*" | "/" | "%" | "^"  }
    FieldOp { ComparisonOp | ScalarOp | tilde }

    Parent { "parent" }
    Resource { "resource" }
    Span { "span" }

    LineComment { "//" ![\n]* }

    @precedence { Parent, Resource, Span, Identifier }

    // Required to avoid ambiguity between "/" and "//"
    @precedence { LineComment, ScalarOp, FieldOp }
}

@skip {
    // `BlockComment` is needed also here
    whitespace | LineComment | BlockComment
}

@skip {} {
  BlockComment { "/*" (blockCommentContent | blockCommentNewline)* blockCommentEnd }
}

@local tokens {
  blockCommentEnd { "*/" }
  blockCommentNewline { "\n" }
  @else blockCommentContent
}
